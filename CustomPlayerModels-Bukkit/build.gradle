plugins {
    id 'java-library' 
    id "fr.il_totore.manadrop" version "0.4.1-SNAPSHOT"
}

version = project.mod_version
group = 'com.yourname.modid' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'CustomPlayerModels-Bukkit'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly "io.netty:netty-all:4.1.25.Final"
}

sourceSets {
	shared {
		java {
			srcDir "../CustomPlayerModels/src/shared/java"
		}
		resources {
			srcDir "../CustomPlayerModels/src/shared/resources"
		}
	}
	main {
        java.srcDirs += shared.java.srcDirs
        resources.srcDirs += shared.resources.srcDirs
    }
}

spigot {
    desc {
        named 'CustomizablePlayerModels' //Default: the project name
        version project.version //Default: the project version
        authors 'tom5454'
        main 'com.tom.cpm.bukkit.CPMBukkitPlugin'
        description 'Customizable Player Models server-side plugin.'
        apiVersion '1.13'
        command {
        	named 'cpm'
            description 'Customizable Player Models command'
            usage '/<command> setskin'
            permission 'cpm.command'
        }
        permission {
        	named 'cpm.*'
        	child {
        		named 'cpm.command'
                inherit true
        	}
        }
        permission {
            named 'cpm.command'
            description 'access to cpm command'
            defaultType 'grant_op'
        }
    }
}

processResources.finalizedBy(spigotPlugin)

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": project.name,
            "Specification-Vendor": "tom5454",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" : "tom5454",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
