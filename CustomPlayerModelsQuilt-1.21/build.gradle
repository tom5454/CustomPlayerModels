plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

base {
	archivesName = project.archives_base_name
}

version = "$project.mod_version"
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.terraformersmc.com/releases/"}
	maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
	accessWidenerPath = file("src/main/resources/cpm.accesswidener")
	// Loom and Loader both use this block in order to gather more information about your mod.
	mods {
		// This should match your mod id.
		"cpm" {
			// Tell Loom about each source set used by your mod here. This ensures that your mod's classes are properly transformed by Loader.
			sourceSet("main")
			// If you shade (directly include classes, not JiJ) a dependency into your mod, include it here using one of these methods:
			// dependency("com.example.shadowedmod:1.2.3")
			// configuration("exampleShadedConfigurationName")
		}
	}
}

def useLib = System.getProperty("useLib")

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		mappings "org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:intermediary-v2"
		officialMojangMappings()
	}
	modImplementation libs.quilt.loader

	// QSL is not a complete API; You will need Quilted Fabric API to fill in the gaps.
	modImplementation libs.bundles.quilted.fabric.api
	// modImplementation libs.bundles.quilted.fabric.api // If you wish to use Fabric API's deprecated modules, you can replace the above line with this one
	
	modImplementation("com.terraformersmc:modmenu:11.0.0-beta.1") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc")
    }
	
	modImplementation ('com.github.Virtuoel:Pehkui:3.8.3') {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc")
    }
	
	compileOnly files("../CustomPlayerModels-1.21/stubs/of.jar")
	
	modCompileOnly('maven.modrinth:vivecraft:1.21.1-1.2.0-fabric')
    implementation "com.electronwill.night-config:core:3.6.3"
    implementation "com.electronwill.night-config:toml:3.6.3"
    implementation("org.lwjgl:lwjgl-openvr:3.3.3")
    implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-linux")
    implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-macos")
    implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-windows")
	implementation("com.illposed.osc:javaosc-core:0.9")
	
	if (useLib) {
		modCompileOnly('maven.modrinth:sodium:mc1.20.5-0.5.8')
    	modCompileOnly('maven.modrinth:iris:1.7.0+1.20.5')
    	modCompileOnly('maven.modrinth:first-person-model:51Nzrb3j')//1.20.4 fallback
	} else {
		compileOnly files("../CustomPlayerModels-1.21/stubs/iris.jar")
		compileOnly files("../CustomPlayerModels-1.21/stubs/fpm.jar")
	}
}

sourceSets {
	main {
		java {
			srcDir "../CustomPlayerModels/src/shared/java"
			srcDir "../CustomPlayerModels-1.21/src/platform-shared/java"
			srcDir "../CustomPlayerModelsFabric-1.21/src/rawmc-shared/java"
		}
		resources {
			srcDir "../CustomPlayerModels/src/shared/resources"
			srcDir "../CustomPlayerModels-1.21/src/platform-shared/resources"
			srcDir "../CustomPlayerModelsFabric-1.21/src/rawmc-shared/resources"
			exclude "**/*.lang"
		}
	}
}

processResources {
	inputs.properties 'version': version, 'group': project.group

	filesMatching('quilt.mod.json') {
		expand 'version': version, 'group': project.group
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 21
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from('LICENSE.md') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
