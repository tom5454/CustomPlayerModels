plugins {
	id 'fabric-loom' version '1.7.bta'
	id 'java'
}

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.4"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	noIntermediateMappings()
	customMinecraftMetadata.set("https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/v${project.minecraft_version}/manifest.json")
}

repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" }
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'SignalumMavenInfrastructure'
		url = 'https://maven.thesignalumproject.net/infrastructure'
	}
	maven {
		name = 'SignalumMavenReleases'
		url = 'https://maven.thesignalumproject.net/releases'
	}
	ivy {
		url = "https://github.com/Better-than-Adventure"
		patternLayout {
			artifact "[organisation]/releases/download/v[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/client.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-server/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/server.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://piston-data.mojang.com"
		patternLayout {
			artifact "v1/[organisation]/[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
}

loom {
	accessWidenerPath = file("src/main/resources/cpm.accesswidener")
}

sourceSets {
	main {
		java {
			srcDir "../CustomPlayerModels/src/shared/java"
			exclude "com/tom/cpl/util/NettyByteBufInputStream.java"
			srcDir "../CustomPlayerModels-1.6/src/retro/java"
			exclude "com/tom/cpl/command/BrigadierCommandHandler.java"
		}
		resources {
			srcDir "../CustomPlayerModels/src/shared/resources"
			exclude "assets/*/lang/*.json"
			srcDir "../CustomPlayerModels-1.6/src/retro/resources"
		}
	}
}

dependencies {
	minecraft "::${project.minecraft_version}"
	mappings loom.layered() {}

	modRuntimeOnly "objects:client:43db9b498cb67058d2e12d394e6507722e71bb45" // https://piston-data.mojang.com/v1/objects/43db9b498cb67058d2e12d394e6507722e71bb45/client.jar
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Helper library
	// If you do not need Halplibe you can comment this line out or delete this line
	//implementation("turniplabs:halplibe:${project.halplibe_version}")

	modImplementation("turniplabs:modmenu-bta:${project.mod_menu_version}")

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"

	implementation 'com.google.guava:guava:33.0.0-jre'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	var log4jVersion = "2.20.0"
	implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}")

	modImplementation("com.github.zarzelcow:legacy-lwjgl3:1.0.4")
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	implementation "org.lwjgl:lwjgl:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-assimp:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"
	
	include(implementation("com.google.guava:failureaccess:1.0.2"))//Required depenency for Guava, that's missing in the Prism install for some reason...
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.release.set 8
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}