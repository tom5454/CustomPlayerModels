plugins {
	alias libs.plugins.loom
	alias libs.plugins.lwjgl
	id 'java'
}

base {
	archivesName = project.archives_base_name
} 
version = project.mod_version
group = project.maven_group

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

loom {
	customMinecraftMetadata.set("https://downloads.betterthanadventure.net/bta-client/${libs.versions.btaChannel.get()}/v${libs.versions.bta.get()}/manifest.json")
}

repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" }
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'SignalumMavenInfrastructure'
		url = 'https://maven.thesignalumproject.net/infrastructure'
	}
	maven {
		name = 'SignalumMavenReleases'
		url = 'https://maven.thesignalumproject.net/releases'
	}
	ivy {
		url = "https://github.com/Better-than-Adventure"
		patternLayout {
			artifact "[organisation]/releases/download/v[revision]/[module].jar"
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-client/${libs.versions.btaChannel.get()}/"
		patternLayout {
			artifact "/v[revision]/client.jar"
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-server/${libs.versions.btaChannel.get()}/"
		patternLayout {
			artifact "/v[revision]/server.jar"
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://piston-data.mojang.com"
		patternLayout {
			artifact "v1/[organisation]/[revision]/[module].jar"
		}
		metadataSources { artifact() }
	}
}

import com.smushytaco.lwjgl_gradle.Preset

lwjgl {
	version = libs.versions.lwjgl
	implementation([Preset.MINIMAL_OPENGL] as Iterable, configurations.runtimeOnly)
}

loom {
	accessWidenerPath = file("src/main/resources/cpm.accesswidener")
}

sourceSets {
	main {
		java {
			srcDir "../CustomPlayerModels/src/shared/java"
			exclude "com/tom/cpl/util/NettyByteBufInputStream.java"
			srcDir "../CustomPlayerModels-1.6/src/retro/java"
			exclude "com/tom/cpl/command/BrigadierCommandHandler.java"
		}
		resources {
			srcDir "../CustomPlayerModels/src/shared/resources"
			exclude "assets/*/lang/*.json"
			srcDir "../CustomPlayerModels-1.6/src/retro/resources"
		}
	}
}

dependencies {
	minecraft "::${libs.versions.bta.get()}"

	runtimeOnly(libs.clientJar)
	implementation(libs.loader)
	// If you do not need Halplibe you can comment out or delete this line.
	implementation(libs.halplibe)
	implementation(libs.modMenu)
	implementation(libs.legacyLwjgl)

	implementation(libs.slf4jApi)
	implementation(libs.guava)
	implementation(libs.log4j.slf4j2.impl)
	implementation(libs.log4j.core)
	implementation(libs.log4j.api)
	implementation(libs.log4j.api12)
	implementation(libs.gson)

	implementation(libs.commonsLang3)
	
	include(implementation("com.google.guava:failureaccess:1.0.2"))//Required depenency for Guava, that's missing in the Prism install for some reason...
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.release.set 8
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

configurations.configureEach { exclude group: "org.lwjgl.lwjgl" }